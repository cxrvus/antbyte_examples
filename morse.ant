use "lib/time";

set fps = 16;
set border = wrap;
set height = 8;
set width = 64;
set start = mid_left;
set colors = bin;

ant main {
	# spawn block on step 1 and cursor on step 3, then die
	A1 = or(on(0, 1), on(1, 1));
	(A0, AX) = cpy(on(1, 1));
}

ant block = 2 { DX = 1; }

fn on_unit = (t6, t5, t4, t3, t2) => out {
	out = and(
		eq(T6, t6),
		eq(T5, t5),
		eq(T4, t4),
		eq(T3, t3),
		eq(T2, t2),

		and(-T1, T0)
	);
}

ant cursor = 3 {
	A2 = or(
		on_unit(0, 0, 0, 0, 0),
		on_unit(0, 0, 1, 0, 0),
		on_unit(0, 1, 0, 0, 1),
		on_unit(0, 1, 0, 1, 0),
		on_unit(0, 1, 0, 1, 1),
		on_unit(0, 1, 1, 1, 0),
		on_unit(1, 0, 1, 0, 0),
		0
	);

	A3 = or(
		on_unit(0, 0, 0, 0, 1),
		on_unit(0, 0, 0, 1, 1),
		on_unit(0, 0, 1, 1, 0),
		on_unit(0, 1, 0, 0, 0),
		on_unit(0, 1, 1, 0, 1),
		on_unit(0, 1, 1, 1, 1),
		on_unit(1, 0, 0, 0, 0),
		on_unit(1, 0, 0, 1, 0),
		0
	);

	A4 = or(
		on_unit(0, 0, 0, 1, 0),
		on_unit(0, 0, 1, 0, 1),
		on_unit(0, 0, 1, 1, 1),
		on_unit(0, 1, 1, 0, 0),
		on_unit(1, 0, 0, 0, 1),
		on_unit(1, 0, 0, 1, 1),
		on_unit(1, 0, 1, 0, 1),
		0
	);

	AX = on_unit(1, 0, 1, 0, 1);
}

fn symbol = () => () {
	AK = and(-M3, CX); # kill target (if first target)
	D2 = or(-M2, AK); # flip on first tick and after killing target
	M2 = 1;
	M3 = or(M3, AK);
	AX = T6;
}

ant dit = 4 {
	() = symbol();
	C = AK;
	DX = M0;
	M0 = or(M0, AK);
}

ant dah = 8 { 
	() = symbol();
	(idle, M1, M0) = add(M1, M0, 0, and(M3, -M4));
	M4 = or(M4, idle);
	C = and(M3, -M4);
	DX = M4;
}

ant space = 16 {
	() = dit();
	C = 0;
}
